#!/bin/sh
#|-*- mode:lisp -*-|#
#| Generate C source code for all defined textures.
exec ros -Q -- $0 "$@"
|#
(progn
  (ros:ensure-asdf)
  (ql:quickload '(:imago) :silent t))

;;; Output --------------------------------------------------------------------

(defparameter +textures-to-generate+
  '(("TEXTURE_EMPTY"          . "textures/empty.png")
    ("TEXTURE_SOLID"          . "textures/solid.png")
    ("TEXTURE_STATIC"         . "textures/static.png")
    ("TEXTURE_UPGRADE_RANGE"  . "textures/upgrade-range.png")
    ("TEXTURE_UPGRADE_BOMBS"  . "textures/upgrade-bombs.png")
    ("TEXTURE_UPGRADE_SPEED"  . "textures/upgrade-speed.png")
    ("TEXTURE_EXPLOSION"      . "textures/explosion.png")
    ("TEXTURE_BOMB_PLAYER_1"  . "textures/bomb-player-1.png")
    ("TEXTURE_BOMB_PLAYER_2"  . "textures/bomb-player-2.png")
    ("TEXTURE_PLAYER_1_DOWN"  . "textures/player-1-down.png")
    ("TEXTURE_PLAYER_1_UP"    . "textures/player-1-up.png")
    ("TEXTURE_PLAYER_1_RIGHT" . "textures/player-1-right.png")
    ("TEXTURE_PLAYER_1_LEFT"  . "textures/player-1-left.png")
    ("TEXTURE_PLAYER_2_DOWN"  . "textures/player-2-down.png")
    ("TEXTURE_PLAYER_2_UP"    . "textures/player-2-up.png")
    ("TEXTURE_PLAYER_2_RIGHT" . "textures/player-2-right.png")
    ("TEXTURE_PLAYER_2_LEFT"  . "textures/player-2-left.png")
    ("TEXTURE_ERROR"          . "textures/error.png")
    ("TEXTURE_UI"             . "textures/ui.png")
    ;; Characters
    ("TEXTURE_CHARACTER_0"    . "textures/character-0.png")
    ("TEXTURE_CHARACTER_1"    . "textures/character-1.png")
    ("TEXTURE_CHARACTER_2"    . "textures/character-2.png")
    ("TEXTURE_CHARACTER_3"    . "textures/character-3.png")
    ("TEXTURE_CHARACTER_4"    . "textures/character-4.png")
    ("TEXTURE_CHARACTER_5"    . "textures/character-5.png")
    ("TEXTURE_CHARACTER_6"    . "textures/character-6.png")
    ("TEXTURE_CHARACTER_7"    . "textures/character-7.png")
    ("TEXTURE_CHARACTER_8"    . "textures/character-8.png")
    ("TEXTURE_CHARACTER_9"    . "textures/character-9.png")))

(defparameter +texture-sizes+
  '(((16 . 16)  . 0)
    ((80 . 120) . 1)
    ((5  . 8)   . 2)))

(defparameter +file-header+ "
/*
 * This file was generated by scripts/generate-textures. DO NOT EDIT!
 */

#include <pleasant-lcd.h>
#include \"texture.h\"
")

(defparameter +texture-definition-template+ "
const PROGMEM texture_t ~A = {
~A};
")

;;; Image conversion ----------------------------------------------------------

(defun texture-size-index (width height)
  (let ((index (cdr (assoc (cons width height)
                           +texture-sizes+
                           :test #'equal))))
    (prog1 index
      (unless index
        (error "Invalid image size ~Ax~A" width height)))))

(defun image-colors (image)
  (loop with colors = ()
        for x below (imago:image-width image)
        do (loop for y below (imago:image-height image)
                 do (pushnew (imago:image-pixel image x y)
                             colors))
        finally (return colors)))

(defun texture-header (size color-count bit-size)
  (format nil "~
TEXTURE_HEADER(~A, ~A, TEXTURE_UNIT_SIZE_~A_BITS, TEXTURE_ENCODING_PLAIN)"
          size
          color-count
          bit-size))

(defun texture-colors (colors)
  (with-output-to-string (out)
    (loop for color in colors
          collect (format out "  RGB(~A, ~A, ~A),~%"
                          (imago:color-red color)
                          (imago:color-green color)
                          (imago:color-blue color)))))

(defun texture-pixels (image colors bit-size)
  (with-output-to-string (out)
    (format out " ")
    (loop
      with bit-count = 0
      for y below (imago:image-height image)
      do (loop
           for x below (imago:image-width image)
           for color-index = (position (imago:image-pixel image x y)
                                       colors)
           do
              (when (zerop bit-count)
                (format out " 0b"))
              (format out "~v,'0b" bit-size color-index)
              (incf bit-count bit-size)
              (when (<= 16 bit-count)
                (format out ",")
                (setf bit-count 0)))
         (when (zerop bit-count)
           (terpri out)
           (when (< y (1- (imago:image-height image)))
             (princ " " out)))
      finally (unless (or (zerop bit-count)
                          (= 16 bit-count))
                (loop repeat (- 16 bit-count)
                      do (format out "0"))))))

(defun image->texture-content (input-path)
  (let* ((image (imago:read-image input-path))
         (colors (image-colors image))
         (color-count (length colors))
         (bit-size (cond ((<= color-count 2)   1)
                         ((<= color-count 4)   2)
                         ((<= color-count 16)  4)
                         ((<= color-count 256) 8)
                         (t (error "Too many colors: ~A" color-count)))))
    (format nil "  ~A,~%~A~A"
            (texture-header (texture-size-index (imago:image-width image)
                                                (imago:image-height image))
                            color-count
                            bit-size)
            (texture-colors colors)
            (texture-pixels image colors bit-size))))

(defun image->texture-definition (name input-path)
  (format nil +texture-definition-template+
          name
          (image->texture-content input-path)))

;;; Main ----------------------------------------------------------------------

(defun main (&rest argv)
  (unless (= 1 (length argv))
    (format *error-output* "Usage: generate-textures OUTPUT-FILE~%")
    (return-from main 1))
  (loop for (name . file) in +textures-to-generate+
        unless (probe-file file)
          do (format *error-output*
                     "Input file ~A does not exist!~%"
                     file)
             (return-from main 2))
  (with-open-file (out (first argv)
                       :direction :output
                       :if-exists :supersede)
    (format out "~A~{~A~}"
            +file-header+
            (loop for (name . file) in +textures-to-generate+
                  collect (image->texture-definition name file)))))
